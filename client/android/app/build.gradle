plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.5.0'
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

def releaseKeyAlias = keystoreProperties.getProperty('keyAlias')
if (releaseKeyAlias == null) {
    releaseKeyAlias = System.getenv('ANDROID_KEYSTORE_KEY_ALIAS')
}

def releaseStorePassword = keystoreProperties.getProperty('storePassword')
if (releaseStorePassword == null) {
    releaseStorePassword = System.getenv('ANDROID_KEYSTORE_PASSWORD')
}

def releaseStoreFile = keystoreProperties.getProperty('storeFile')
if (releaseStoreFile == null) {
    releaseStoreFile = System.getenv('ANDROID_KEYSTORE_PATH')
}

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', 'HEAD'
        standardOutput = stdout
    }
    return "\"" + stdout.toString().trim() + "\""
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "uk.hasali.zenith"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        buildConfigField "String", "GIT_COMMIT_HASH", getGitHash()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        release {
            keyAlias releaseKeyAlias
            keyPassword releaseStorePassword
            storeFile releaseStoreFile ? file(releaseStoreFile) : null
            storePassword releaseStorePassword
        }
    }

    buildFeatures {
        compose true
    }

    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix '.debug'
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.0.0-beta09'
    }
}

dependencies {
    def compose_version = '1.0.0-beta09'
    def accompanist_version = '0.12.0'
    def ktor_version = '1.6.0'
    def exoplayer_version = '2.14.1'

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.2.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-datetime:0.2.0'

    implementation 'androidx.activity:activity-compose:1.3.0-beta02'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.core:core-ktx:1.5.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.0-alpha02'

    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation "androidx.compose.foundation:foundation:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.material:material-icons-core:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"

    implementation "com.google.accompanist:accompanist-coil:$accompanist_version"
    implementation "com.google.accompanist:accompanist-insets:$accompanist_version"

    implementation "io.ktor:ktor-client-core:$ktor_version"
    implementation "io.ktor:ktor-client-android:$ktor_version"
    implementation "io.ktor:ktor-client-serialization:$ktor_version"

    implementation "com.google.android.exoplayer:exoplayer-core:$exoplayer_version"
    implementation "com.google.android.exoplayer:exoplayer-ui:$exoplayer_version"

    implementation 'com.google.android.material:material:1.3.0'
}
