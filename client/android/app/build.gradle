plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'org.jetbrains.kotlin.kapt'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.7.10'
    id 'dagger.hilt.android.plugin'
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

def releaseKeyAlias = keystoreProperties.getProperty('keyAlias')
if (releaseKeyAlias == null) {
    releaseKeyAlias = System.getenv('ANDROID_KEYSTORE_KEY_ALIAS')
}

def releaseStorePassword = keystoreProperties.getProperty('storePassword')
if (releaseStorePassword == null) {
    releaseStorePassword = System.getenv('ANDROID_KEYSTORE_PASSWORD')
}

def releaseStoreFile = keystoreProperties.getProperty('storeFile')
if (releaseStoreFile == null) {
    releaseStoreFile = System.getenv('ANDROID_KEYSTORE_PATH')
}

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def accompanist_version = '0.26.4-beta'
def compose_version = '1.3.0-beta03'
def compose_compiler_version = '1.3.1'

android {
    compileSdkVersion 33
    namespace 'uk.hasali.zenith'

    defaultConfig {
        applicationId "uk.hasali.zenith"
        minSdkVersion 23
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"

        resValue "string", "GIT_COMMIT_HASH", getGitHash()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        release {
            keyAlias releaseKeyAlias
            keyPassword releaseStorePassword
            storeFile releaseStoreFile ? file(releaseStoreFile) : null
            storePassword releaseStorePassword
        }
    }

    buildFeatures {
        compose true
    }

    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix '.debug'
            resValue 'string', 'app_name', 'Zenith (d)'
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            resValue 'string', 'app_name', 'Zenith'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
    }

    composeOptions {
        kotlinCompilerExtensionVersion compose_compiler_version
    }
}

kapt {
    correctErrorTypes true
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'
    implementation 'org.jetbrains.kotlinx:kotlinx-datetime:0.4.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.4.0'

    implementation 'com.google.dagger:hilt-android:2.43'
    kapt 'com.google.dagger:hilt-compiler:2.43'

    implementation 'androidx.activity:activity-compose:1.6.0'
    implementation 'androidx.datastore:datastore-preferences:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.1'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.5.1"
    implementation 'androidx.mediarouter:mediarouter:1.3.1'

    implementation "androidx.media3:media3-exoplayer:1.0.0-beta02"
    implementation "androidx.media3:media3-session:1.0.0-beta02"
    implementation "androidx.media3:media3-ui:1.0.0-beta02"

    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation "androidx.compose.foundation:foundation:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.material:material-icons-core:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"

    implementation "com.google.accompanist:accompanist-swiperefresh:$accompanist_version"

    implementation 'io.coil-kt:coil-compose:2.1.0'

    implementation 'com.squareup.okhttp3:okhttp:4.10.0'
    implementation 'com.squareup.okhttp3:okhttp-sse:4.10.0'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.jakewharton.retrofit:retrofit2-kotlinx-serialization-converter:0.8.0'

    implementation 'com.google.android.gms:play-services-cast-framework:21.1.0'
}
