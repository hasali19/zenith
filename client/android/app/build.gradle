plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.5.0'
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

def releaseKeyAlias = keystoreProperties.getProperty('keyAlias')
if (releaseKeyAlias == null) {
    releaseKeyAlias = System.getenv('ANDROID_KEYSTORE_KEY_ALIAS')
}

def releaseStorePassword = keystoreProperties.getProperty('storePassword')
if (releaseStorePassword == null) {
    releaseStorePassword = System.getenv('ANDROID_KEYSTORE_PASSWORD')
}

def releaseStoreFile = keystoreProperties.getProperty('storeFile')
if (releaseStoreFile == null) {
    releaseStoreFile = System.getenv('ANDROID_KEYSTORE_PATH')
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "uk.hasali.zenith"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        release {
            keyAlias releaseKeyAlias
            keyPassword releaseStorePassword
            storeFile releaseStoreFile ? file(releaseStoreFile) : null
            storePassword releaseStorePassword
        }
    }

    buildFeatures {
        compose true
    }

    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix '.debug'
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
        useIR = true
    }

    composeOptions {
        kotlinCompilerExtensionVersion compose_version
        kotlinCompilerVersion '1.4.32'
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.2.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-datetime:0.2.0'

    implementation 'androidx.activity:activity-compose:1.3.0-alpha07'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.0-alpha01'

    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation "androidx.compose.foundation:foundation:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.material:material-icons-core:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"

    implementation 'com.google.accompanist:accompanist-coil:0.8.1'
    implementation 'com.google.accompanist:accompanist-insets:0.8.1'

    implementation "io.ktor:ktor-client-core:1.5.3"
    implementation "io.ktor:ktor-client-android:1.5.3"
    implementation "io.ktor:ktor-client-serialization:1.5.3"

    implementation 'com.zachklipp:compose-backstack:0.9.0'

    implementation 'com.google.android.exoplayer:exoplayer-core:2.13.3'
    implementation 'com.google.android.exoplayer:exoplayer-ui:2.13.3'

    implementation 'com.google.android.material:material:1.3.0'
}
